use uri
use timestamp

/// Pattern matching a well-formed OpenVEX document.
pattern valid = {
    @context: uri::url,
    @id: uri::iri,
    author: string,
    role: string,
    timestamp: timestamp::iso8601,
    version: string,
    tooling?: string,
    statements?: list::all<statement>,
}

pattern statement = {
    vulnerability: string, // TODO: Strong type
    vuln_description?: string,
    timestamp?: string, // TODO: Strong type
    products?: list::all<string>, // TODO: Strong type
    subcomponents?: list::all<string>, // TODO: Strong type
    status: "not_affected" || "affected" || "fixed" || "under_investigation",
    status_notes?: string,
    justification?: "component_not_present" || "vulnerable_code_not_present" || "vulnerable_code_not_in_execute_path" || "vulnerable_code_cannot_be_controlled_by_adversary" || "inline_mitigations_already_exist",
    impact_statement?: string,
    action_statement?: string,
    action_statement_timestamp?: string,
}

/// Enforce that there are no statements considered affecting.

#[reason("One or more dependencies are affected")]
#[authoritative]
pattern not-affected = {
    statements?: list::none<{
        status: "affected" || "under_investigation"
    }>,
}

/// Match on any statements listed as affected.
pattern affected = {
    statements: list::any<{
        status: "affected" || "under_investigation"
    }>,
}